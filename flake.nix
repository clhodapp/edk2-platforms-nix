# SPDX-License-Identifier: MIT
{

  description = "edk2-platforms built against edk2 stable tags with nixpkgs' toolchain: drop-in UEFI drivers released for x86-64 and AArch64, and the rest as build attributes";

  # Honored only when this flake is evaluated directly (`nix build`,
  # `nix flake check`) and the settings are accepted: answer the prompt,
  # or pass `--accept-flake-config` (a non-interactive run otherwise
  # ignores them with a warning). A consumer that takes this flake as an
  # input gets nothing from it and must declare the caches itself. The
  # two upstreams are part of the deal: the clhodapp cache skips
  # uploading paths they already hold.
  nixConfig = {
    extra-substituters = [
      "https://clhodapp.cachix.org"
      "https://nix-community.cachix.org"
      "https://numtide.cachix.org"
    ];
    extra-trusted-public-keys = [
      "clhodapp.cachix.org-1:EW/0conxH0OQyo0o4ub/grdkFspholmQMSnQyj0vrZI="
      "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs="
      "numtide.cachix.org-1:2ps1kLBUWjxIneOy1Ik6cQjb41X0iXVXeHigGmycPPE="
    ];
  };

  inputs = {
    caisson.url = "github:nix-caisson/caisson";

    # Stable channel on purpose: a boot-path firmware artifact should churn
    # as little as possible, and nothing here needs bleeding-edge nixpkgs.
    # Only the toolchain and the edk2 package recipe (BaseTools build,
    # source de-vendoring) come from here; the edk2 trees are pinned below.
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-26.05";

    # The edk2 core the released driver is built against, at a stable
    # tag; the tag is the driver's upstream version (see
    # pkgs/ext4-dxe/ext4-dxe/version.nix). The release workflow moves
    # this ref to each new edk2-stable tag. Without submodules: they
    # hold the crypto and test libraries (OpenSSL alone is over half
    # the tree) and nothing built here links or runs them; the one
    # BaseTools program that needs one is left out of the build (see
    # pkgs/ext4-dxe/default.nix).
    edk2-src.url = "git+https://github.com/tianocore/edk2?ref=refs/tags/edk2-stable202608&shallow=1";
    edk2-src.flake = false;

    # edk2 master, for the `unstable` packages; advanced weekly, never
    # released.
    edk2-unstable-src.url = "git+https://github.com/tianocore/edk2?ref=refs/heads/master&shallow=1";
    edk2-unstable-src.flake = false;

    # The driver's source tree; only Features/Ext4Pkg is used. Pinned in
    # the lock and advanced weekly; a move that changes the built driver
    # bumps the release revision automatically.
    edk2-platforms.url = "github:tianocore/edk2-platforms";
    edk2-platforms.flake = false;
  };

  outputs =
    inputs@{ caisson, ... }:
    let
      lib = caisson.lib.caisson-core.mkLib {
        inherit inputs;

        projects = {
          inherit caisson;
        };

        modules = lib: {
          flake = {
            default = lib.caisson.flake-parts.mkModule ./modules/flake-parts/default;
          };
        };

        libOverlays = mkLibOverlay: {
          default = mkLibOverlay ./lib-overlays/default;
        };
      };
    in
    lib.caisson.flake-parts.mkConfiguration {
      name = "edk2-platforms-nix";
      configModule = lib.caisson.flake-parts.mkModule ./configs/flake-parts/default;
    };

}
